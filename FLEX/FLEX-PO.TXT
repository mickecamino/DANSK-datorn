 OPT PAG
 PAG

 TTL Flex f|r danskdatorn.
 STTL Skriver tid till filer, och l{ser {ven '[,\,],^'.

* Flex oprerating system Version H.D Modified
*
* Revision history :
* 04/06/84 BGS - CCP 2.8 Mod and FMS 2.8 Mod
* 12/06/84 BGS - Version header for VER utillity
*                Tree structure in CLASS
* 21/06/84 BGS - Implementation of MAPUP in GETFIL
*                PROMT changed to pointer
*                Lower-case messages
*                Change in PAUSE for interrupt driven I/O
* 24/06/84 BGS - Addition of interrupt vector setup
*                 in Flex coldstart
* 08/07/84 BGS - Save of Y-register in LDBIN for
*                 XBASIC interface (It's line pointer)
* 21/04/85 H.D.  Change in Disk Driver and IO DC-Computer
*
* 30/05/87 POR   Correction of searchroutine for epromcommands
*                change of directoryeprom size (27256)
*                symbol EPROM12 added
*
* 02/01/88 POR   Change instr after label PCMLIN from BSR to JSR
*                for adapting cmdrpt routine
* 10/09/89 BB    Initiering av ACIOR vid uppstart. ( SERIS )


VER EQU 2 Version number
REV EQU 9 Revision number

** DC4-BUG

SYSBYT EQU $E742
RAMSIZ EQU $E76D
TRACKS EQU $E76F
MAXRAM EQU $E76C
PRTBOT EQU $E750
PBANK1 EQU $E752
PBANK2 EQU $E755
PRTTOP EQU $E751
PRTSZ1 EQU $E760
PRTSZ2 EQU $E762
SPOUT  EQU $E756
SPIND  EQU $E753
PIOLST EQU $E75C
PFLAG1 EQU $E7A9
PFLAG2 EQU $E7AA
PRESC1 EQU $E766
PRESC2 EQU $E767
PLSTCH EQU $E75B
PRTDP  EQU $E764
PRTLNG EQU $E74F
PRTEJ  EQU $E765
PRTANT EQU $E758
RTCBYT EQU $E7B9
TIME   EQU $E772
WEEKD  EQU $E784
WEEKN  EQU $E786
RTCTIM EQU $E77B
WRKSPC EQU $E7C5
SECTOR EQU $E770
RTCLOC EQU $E7DE
BASBY1 EQU $E7AF
BASBY2 EQU $E7B0
BASBY3 EQU $E7B1
TIMCON EQU $E768
GSTR2  EQU $F000
NOESC  EQU $F98B
BERRVC EQU $E7B2
ESCVEC EQU $E7CD
INVEC  EQU $E7CB
INHUP  EQU $F970
ECHO   EQU $DFE2
MOUTCH EQU $F80A
MICHEC EQU $F808
MINCHE EQU $F806
MINCH  EQU $F804
GOUTCH EQU $F755
PGSTR  EQU $F742
RTCSET EQU $FA7C
RTCTIS EQU $FA59
OUTCHR EQU $F022
OUT2H  EQU $F725
PDATA0 EQU $F70C
SETRTC EQU $FB95
NORTC  EQU $FB70
SETL25 EQU $FDA9
INCHNO EQU $F7F0
CLRL25 EQU $FDC4
RDUSCM EQU $FC81
SWI3V  EQU $DFC2
IRQV   EQU $DFC8

EPROM12 EQU $0000 byte have be change when changing type eprom
TERM   EQU $E009
PROMNR EQU $E022
PRMADR EQU $E020
PRMDAT EQU $E023
VIA    EQU $E070
PORTA  EQU $E071  I/O PORT A
DRAREG EQU $E073  DATAREG. A
VPCR   EQU $E07C  PERI. REG.
IFR    EQU $E07D  INT. FLAG REG.

* ASCII code equates

NUL EQU 0
EOT EQU 4
BS EQU 8
HT EQU 9
LF EQU 10
FF EQU 12
CR EQU 13
CAN EQU $18
ESC EQU $1B
SP EQU $20

* DISK ROUTINER FOR DC-BOARD
*
FLOOPC EQU $E010
DRVREG EQU $E014
COMREG EQU $E018
TRKREG EQU $E019
SECREG EQU $E01A
DATREG EQU $E01B

FMSBUS EQU $CC30 FLEX ALREADY BUSY
DISKIO EQU $FE76
VDRIVE  EQU $FE5D
VCOLD  EQU $FECE

* Data area definition

 ORG $C000

 RMB 128
STACK EQU * System stack

LINBUF RMB 127 Command line buffer
LINEND RMB 1 Buffer end

UTILTY EQU * Utility program area start

 ORG $C400

BOTBEG BRA LC407
 FCB $82,$2E,$88,$3A,$83

LC407 LDA #$39
 STA FLXINI
 LDD #WARMS
 STD ESCRET
 LDD TCHECK
 STD STAT+1
 LDD TOUTCH
 STD OUTCH+1
 STD OUTCH2+1
 LDD TINCHE
 STD INCH+1
 STD INCH2+1
 JSR [TRMINT]
 JSR PCRLF
 JSR POVER
 LDA #0
 STA PRTLNG
 STA PRTDP
 LDA #8             EJ
 STA PRTEJ
 CLR PFLAG1
 TST RTCBYT
 BNE DATENT
 LDA SYSBYT
 CMPA #$5A
 BEQ NORTM
 JSR RTCTIS
NORTM JSR PCRLF
 LDD PROMTX
 BRA NODAT
DATENT LDD PROMTX
 PSHS A,B
 LDX #DATMSG
 STX PROMTX
 JSR PSTRNG
 JSR INBUFF
 PULS A,B
 STD PROMTX
 LDY #SYSMTH
 JSR CHKDAT
 BLS DATENT
 JSR CHKDAT
 BLS DATENT
 JSR CHKDAT
 BLS DATENT
 JSR PCRLF
 JSR PCRLF
NODAT LDX #VMGS1
 JSR PDATA0
 LDX #SECTOR
 CLRB
 JSR OUTDEC
 LDX #VMGS2
 JSR PDATA0
 LDX #PRTSZ1
 CLRB
 JSR OUTDEC
 LDX #PRTMGS
 JSR PDATA0
 TST RTCBYT
 BNE INLAB0
 LDX #$EBC4
 STX RTCLOC
 JSR SETRTC
INLAB0 LDA SYSBYT
 CMPA #$5A
 BNE NODAT2
 LDB #$90
INLAB1 LDX #$7000
INLAB DEX
 BNE INLAB
 JSR TSTAT
 BNE NODAT2
INLAB2 DECB
 BNE INLAB1
NODAT2 LDA #$5A
 CMPA SYSBYT
 BEQ NODAT3
 STA SYSBYT
 LDA #$0B
 STA $E07C
 LDA #$FF
 STA $E073
NODAT3 LDX #$EFC7
 STX RTCLOC
 TST RTCBYT
 BNE NODATX
 JSR NORTC
NODATX LDA TERM
 CMPA #'q
 BEQ NODAT4
 BHI CHSTUP
 CMPA #'Q
 BNE CHSTUP
NODAT4 JMP WARMS

CHSTUP LDX #SYSFCB
 JSR DCHECK
 LDA #1
 STA 0,X
 JSR FMS
 BEQ STRTUP
 LDA 1,X
 CMPA #4
 BNE STRTER
INDEND JMP WARMS

STRTUP LDY #LINBUF
 STY LINPTR
 LDB #$80

LC485 JSR FMS
 BNE STRTER
 DECB
 BEQ STRTER
 STA 0,Y+
 CMPA #$0D
 BNE LC485
 LDA #4
 STA 0,X
 JSR FMS
 JMP RENTER

CHKDAT JSR INDEC
 PSHS X
 BCS LC4AD
 LDA 0,Y
 TSTB
 BEQ LC4AB
 LDA 1,S

LC4AB STA 0,Y+

LC4AD PULS A,B,PC

STRTER LDX #ERRMSG
 JSR PSTRNG
 JMP WARMS

VMGS1 FCC "Vdisk ",0
VMGS2 FCC " sektorer",$D,$A,$A,0
PRTMGS FCC " K printerbuffert",$D,$A,0
ERRMSG FCC "Kan ej k|ra STARTUP",4

 ORG $C700

SPIRQ JMP PIRQ
SPOL JMP PSPOL

PIRQ ORCC #%01010000
 JSR TSTCLR
 BNE PRT_01
 LBEQ IRQ_NO           JA
PRT_01 JSR $CCEA      TEST PRINTER READY
 BEQ IRQ_DI           NEJ
 LDB PBANK2           B=BANK OUT
 LDX SPOUT            X=POINTER
 STB $FFF0            S[T BANK
 LDA 0,X              HENT DATA TIL PRINTER
 BPL PRT_0
 CMPA #$E0
 BLO PCKSPA
PRT_0 CMPA #$03
 BEQ FILEEN
PRT_1 JSR $CCF0
 STA PIOLST
 LDA #$04
 STA 0,X+
PRT_2 JSR TSTINC
IRQ_P4 LDA $DFD0
 STA $FFF0
IRQ_DI LDA #$0E
 STA $E079
 RTI

PCKSPA DECA
 CMPA #$80
 BEQ PCSP1
 STA 0,X
 LDA #$20
 JSR $CCF0
 BRA IRQ_P4
PCSP1 LDA #$20
 JMP PRT_1

FILEEN PSHS A
 LDA #$1B
 CMPA PIOLST
 BEQ FILE1
 LDA PRTANT
 CMPA #1
 BEQ FILE1
 DEC PRTANT
FILE1 PULS A
 BRA PRT_1

TSTCLR LDB PBANK2           HVIS ENS
 CMPB PBANK1          INGEN DATA TIL PRINTER
 BNE TTCLE            NEJ, PRINT
 LDX SPOUT
 CMPX SPIND           ENS ?
TTCLE RTS

TSTINC JSR CHKKR
 BNE PRT_3
 LDY PRTSZ2
 LEAY 1,Y
 STY PRTSZ2
PRT_3 JSR CHKBNK
 STB PBANK2
 STX SPOUT
 RTS

** TEST FOR PLADS I BUFFER

TSTBUF PSHS CC,B,X
 ANDCC #%10101111
SPO1 LDX SPIND
 LDB PBANK1
 INX
 JSR CHKBNK
SPO2 CMPB PBANK2
 BNE TBUFEN
 CMPX SPOUT
 BNE TBUFEN
 BRA SPO1
TBUFEN PULS CC,B,X,PC

CHKBNK CMPX #$1000
 BNE CHBAR2
 INCB
 CMPB PRTTOP
 BNE CHBAR1
 LDB PRTBOT
CHBAR1 LDX #$0000
CHBAR2 RTS

CHKKR CMPX #$03FF
 BEQ CHKKEN
 CMPX #$07FF
 BEQ CHKKEN
 CMPX #$0BFF
 BEQ CHKKEN
 CMPX #$0FFF
CHKKEN RTS

IRQ_NO STB $FFF0
 LDA 0,X+
 CMPA #$04
 BNE IRQ_N1
 JSR CHKBNK
 LDA 0,X
 CMPA #$04
 BNE IRQ_N1
 JSR [TIMOFF]        S[T TIMER OFF
 CLR PRTANT
 CLR PFLAG1
IRQ_N1 JMP IRQ_P4

SETBUF STA 0,X+
PTLAB2 STA PLSTCH
PTLAB3 JSR CHKKR
 BNE PT_OU1
 LDY PRTSZ2
 LEAY -1,Y
 STY PRTSZ2
PT_OU1 JSR CHKBNK           4K BLOKKE
PRT2 STX SPIND
 STB PBANK1
 RTS

 ORG $C82A

       FCC "HCD",4

DATMSG FCC "Dat: (MM,DD,YY)? ",4

 ORG $C840

SYSFCB FDB $FF00,$0000

FILNAM FCC "startup",0

FILEXT FCC "txt",0

XFER ORCC #$50
 LDS #LINBUF
 JSR SERIS
 LDA #$0B
 STA $E010
 LDA #$09
 STA $E011
 LDX #$BFFF
 STX MEMEND
 LDA #%10100101
 STA CPUTYP
 LDA SYSBYT
 CMPA #$5A
 BNE TSTRAM
NOUSTB JMP COLDS
TSTRAM LDX #$FFF0
 CLR RAMSIZ
 LDY #$0265              NB S[T 0 I VDISK
 LDB #0
TST1 STB 0,X
 LDA 0,Y
 PSHS A
 LDA #$A5
 STA 0,Y
 CMPA 0,Y
 BNE TSTEND
 INC RAMSIZ
 BNE TST2
 LDA #$FF
 PSHS A
 LDD #$0400
 BRA FORTS
TST2 PULS A
 STA 0,Y
 INCB
 BEQ TSTEND
 BRA TST1
TSTEND PULS A
 LDB #4
 LDA RAMSIZ
 PSHS A
 MUL
FORTS STD RAMSIZ
 PULS A
 SUBA #1
 STA TRACKS
 STA MAXRAM
 CMPA #$0F
 BNE TSTE1
 LDA #$0F
 STA PRTBOT
 STA PBANK1
 STA PBANK2
 INCA
 STA PRTTOP
 BRA TSTE2
TSTE1 INCA
 STA PRTTOP
 LDA #$11
 STA PRTBOT
 STA PBANK1
 STA PBANK2
TSTE2 LDA PRTTOP
 SUBA PRTBOT
 LDB #4
 MUL
 STD PRTSZ1
 STD PRTSZ2
 LDA #$00
 STA 0,X
CLDEND JMP COLDS
*
SERIS LDA #3
 STA $E004
 STA $E006
 JMP RTCTIS
*

 ORG $C9C0

PSPOL PSHS CC,B,X,Y
 ORCC #%01010000
 CMPA #$03
 BEQ PT_OU6
 TST PFLAG1
 BNE PT_OU6
 COM PFLAG1
 PSHS A
 JSR [TIMINT]
 PULS A
PT_OU6 JSR TSTBUF
PT_OUT LDB PBANK1
 STB $FFF0
 LDX SPIND
 PSHS A
 LDA PLSTCH
 CMPA #$1B
 BNE PTSSPC
 PULS A
 JSR SETBUF
 BRA PBFEND
PTSSPC PULS A
 CMPA #$20
 BEQ PPSPAC
PTLAB0 TST PRTDP
 BEQ PTLAB1
 CMPA #$0C
 BEQ PRTFM
PTLAB1 JSR SETBUF
 CMPA #$0A
 BEQ PRTLN
PBFEND LDB $DFD0
 STB $FFF0
 PULS CC,B,X,Y,PC

PPSPAC CMPA PLSTCH
 BEQ PSPAC
 BRA PTLAB1

PSLAB1 INX
 BRA PTLAB1

PSPAC LEAX -1,X
 CMPX #$FFFF
 BEQ PSLAB1
 LDA 0,X
 BPL PSPC1
 INCA
 STA 0,X
 BRA PBFEND

PSPC1 LDA #$82
 STA 0,X
 BRA  PBFEND

PRTLN TST PRTDP
 BEQ PBFEND
 DEC PRTLNG-1
 BNE PBFEND
 PSHS A
 LDA PRTEJ
 PSHS A
PTLNN LDA #$0A
 LDB $DFD0
 STB $FFF0
 JSR TSTBUF
 LDB PBANK1
 STB $FFF0
 LDX SPIND
PRTLN1 JSR SETBUF
 DEC 0,S
 BNE PTLNN

 LDA PRTDP          PRINTER L[NGDE
 STA PRTLNG-1
 PULS A
 PULS A
 STA  PLSTCH
 BRA PBFEND

PRTFM PSHS A
 LDA PRTLNG-1
 ADDA PRTEJ
 PSHS A
 BRA PTLNN

 ORG $CA80

ERROR JSR SETL25
 LDX #SYSFCB
 LDA 3,X
 ORA #$30
 JSR OUTCHR
 LDA #'.
 JSR OUTCHR
 LDX #SYSFCB+36
LOP LDA 0,X+
 BEQ LOP1
 JSR OUTCHR
LOP1 CMPX #SYSFCB+44
 BNE LOP2
 LDA #'.
 JSR OUTCHR
LOP2 CMPX #SYSFCB+47
 BNE LOP
 LDA #$20
 JSR OUTCHR
 JSR OUTCHR
 LDX #SYSFCB
 JSR RERR
 LDX #ERTST
 PSHS X
 PSHS CC,A,B,X,Y,U
 JMP $F128 BELL
ERTST JSR INCHNO
 JSR CLRL25
 RTS

 ORG $CAD0

USCMEX LDX UCMDTB
 BNE CMINRM
 JSR SRCHTB
 BNE CMINRM
 JMP EXETAB
CMINRM LDX #EPROM12  $0000 27256, $E000 2764  
 LDA #12
 STA PROMNR
 STX PRMADR
 LDA PRMDAT
 CMPA #$11        SKAL V[RE BNR I MONITOR
 BNE FLXCMD
 PSHS B,Y
 JSR RDUSCM
 PULS B,Y
FLXCMD JMP SRCDIR

** CLEAR TIMER IRQ
*
CLTIRQ LDA TIMCON+1
 STA VIA+9
 RTI

 ORG $CB00

DRINIT LDX #DDENS
 LDB #$0C
DINIT2 CLR 0,X+
 DECB
 BNE DINIT2
 RTS

DRSTOR BSR DRVSEL
 BCS LCB2D
 LDA #8
 STA COMREG
 LBSR DELAY1
 LBSR TBUSY
 ANDCC #$FE
 BITB #$40
 BEQ LCB2D
 LDB #$0B

LCB2D RTS

DRVSEL PSHS X
 LDA 3,X
 CMPA #4
 BCS DRSEL1
 LDB #$1F
 ASRB
 PULS X,PC

DRSEL1 BSR DRSEL4
 LDB TRKREG
 STB 0,X
 STA DRVREG
 CMPA CURDRV
 BEQ DRSEL3
 LDX #$17EA

DRSEL2 LBSR DELAY1
 LEAX -1,X
 BNE DRSEL2

DRSEL3 STA CURDRV
 BSR DRSEL4
 LDA 0,X
 STA TRKREG
 BRA LCB77

DRSEL4 LDX #TRKTBL
 LDB CURDRV
 ABX
 RTS

DRCHEC PSHS X
 LDA 3,X
 STA DRVREG

LCB77 LDX #$9367

LCB84 LDB COMREG
 BMI LCB8C
 CLRA
 PULS X,PC

LCB8C LBSR DELAY1
 LDB COMREG
 BPL LCB9B
 LEAX -1,X
 BNE LCB8C
 CLRA
 ORCC #4
 PULS X,PC

LCB9B LDX #$27DC

LCBA6 LBSR DELAY1
 LEAX -1,X
 BNE LCBA6
 CLRA
 PULS X,PC

 ORG $CB90

NOTFUN JSR SETIO
 LDX #NOTFND
 JMP FNDNTX

WHAT JSR SETIO
 LDX #WHATXT
 JMP ILLCMX

SETIO PSHS D
 LDD LINBUF
 CMPD #$5053  "PS"
 BNE SETI1
 DEC PRTANT
SETI1 LDD OUTCH2+1
 STD OUTCH+1
 PULS D,PC
** BASIC EDITOR CLEAR
*
BASRTN CLR BASBY1
 CLR BASBY2
 CLR BASBY3
 LDX #0
 STX BERRVC
 LDX #NOESC
 STX ESCVEC
 JMP WARMS

CLRTRP CLR SEMFMS
 ANDCC #$AF
SWISE2 RTS

TRAPSW ORCC #$50
 TST SEMFMS
 BNE LC749
 INC SEMFMS
 RTS
LC749 SWI3
 NOP
 RTS

HOPP EQU *
 ANDA #$7
 ANDB #$F
 RPT 4
 ASLA
 ABA
 RTS

 ORG $CC00

TTYBS FCB BS DELETE CHARACTER
TTYDEL FCB CAN DELETE LINE
TTYEOL FCB ': END OF LINE CHARACTER
TTYDP FCB 0  PAGE DEP.
TTYWD FCB 0 LINE WIDH
TTYNL FCB 0 NULLS AFTER CR-LF
TTYTB FCB 0 TAB CHARACTER
TTYBE FCB 8 BACKSPACE ECHO CHARACTER
TTYEJ FCB 0 EJECT COUNT
TTYPS  FCB 0  PAUSE ENABLE FLAG
TTYESC FCB SP ESCAPE CHARACTER

SYSDRV FCB 0 SYSTEM DRIVE NUMBER
WRKDRV FCB 1 WORK DRIVE NUMBER

DEFSYS FCB 0 DEFAULT SYSTEM DRIVE (GETFIL)

SYSMTH FCB 0 MONTH
SYSDAY FCB 0 DAY
SYSYR FCB 0 YEAR

LSTTRM FCB 0 LAST TERMINATOR CHAR
UCMDTB FDB 0 USER COMMAND TABLE VECTOR
LINPTR FDB 0 INPUT LINE POINTER
ESCRET FDB 0 ESCAPE JUMP VECTOR
CURCHR FCB 0 CURRENT CHARACTER
PRVCHR FCB 0 PREVIOUS CHARACTER
CURLIN FCB 0 CURRENT LINE NUMBER
LDROFF FDB 0 LOADER OFFSET
XFERFG FCB 0 LOADER TRANSFER ADDRESS FLAG
XFERAD FDB 0 LOADER TRANSFER ADDRESS
ERRTYP FCB 0 ERROR TYPE
SPECIO FCB 0 SPECIAL I/O FLAG
OUTSW FCB 0 OUTPUT SWITCH
INSW FCB 0 INPUT SWITCH
OUTFIL FDB 0 OUTPUT FILE ADDRESS
INFIL FDB 0 INPUT FILE ADDRESS
CMDFLG FCB 0 COMMAND FLAG
CURCLM FDB 0 CURRENT OUTPUT COLLUMN
MEMEND FDB 0 MEMORY END
ERRVEC FDB 0 ERROR FILE NAME VECTOR
FILEKO FCB 1 FILE INPUT ECHO FLAG

SEMFMS FCB 0 FMS SEMAPHORE EQ=FREE, NE=IN USE
_TASK FDB 0 CURRENT TASK POINTER
CPUTYP FCB 0 TSC CPU board type number
TASK FCB 0 CURRENT TASK NUMBER

 RMB 8 NOT USED ?

 RMB 6 NOT USED
CALLPC RMB 2
CALLSP RMB 2
 RMB 2 NOT USED
MAPUP FCB $60 $60=Map to uppercase, $FF=Off
 RMB 2 NOT USED
NAMFLG RMB 1
 RMB 1 NOT USED

PROMT FDB PROMTX Pointer to promt text
PROMTX FCC '>>>',EOT
 RMB 1 Not used
EscFlg FCB 0 Flag for escape character detect
WHATXT FCC "Vad ?",EOT
NOTFR FCC "Ingen transfer",EOT
NOTFND FCC "Finns ej ",EOT
DSKERR FCC "Diskfel nr: ",EOT
NOTRDY FCC "Floppyn ej klar ",EOT

SYSCMD FCC "GET",0
 FDB GETCMD
 FCC "MON",0
 FDB MONCMD
 FCC "X",0
 FDB EXECMD
 FCB 0

TIDRUT EQU *
 JSR LDDC5   Fanns p} $DA44, d{r JSR TIDRUT finns.
 JSR $FA7C
 LDX CURFCB
 LDD $E77E
 LBSR HOPP
 STA 24,X
 LDD $E77B
 LBSR HOPP
 STA 16,X
 RTS

 ORG $CCC0

PINIT TST PFLAG2
 BNE PIR2
 LDA #$0B  INIT VIA TO HANDSHAKE
 STA VPCR
 LDA #$FF  SET PORT A TO OUTPUT
 STA DRAREG
 STA PFLAG2
 LDA PORTA
 BRA PIR2

 ORG $CCD8

PCHK RTS

PIR2 LDA #$03  START OF FILE
 BSR POUT
 INC PRTANT
 RTS

 ORG $CCE4

POUT JMP $F76E  PRINTER OUT
POUO JMP $C703  PRINTER BUFFER
POU1 LDA IFR    TEST FOR READY
 ANDA #2        SET CC-REG
 RTS
POU2 STA PORTA
 RTS

 ORG $CCF8

TASK00 FCB 1,0 SYSTEM TASK
 FDB 0 SAVED STACK POINTER
TASK01 FCB 0,0 SPOOLER TASK
 FDB 0 IT'S SP

* Flex user entry points

COLDS JMP INITF
WARMS JMP MAIN
RENTER JMP CMDLOP
INCH JMP TINCHE
INCH2 JMP TINCHE
OUTCH JMP TOUTCH
OUTCH2 JMP TOUTCH
GETCHR JMP GETCH
PUTCHR JMP PUTCH
INBUFF JMP GETLIN
PSTRNG JMP PSTR
CLASS JMP CLASCH
PCRLF JMP CRLF
NXTCH JMP NXCH
RSTRIO JMP RESIO
GETFIL JMP GFILN
LOAD JMP LDBIN
SETEXT JMP SEXT
ADDBX JMP ADBX
OUTDEC JMP OTDEC
OUTHEX JMP OTHEX
* RPTERR JMP RERR       ** HD  [NDRET FOR DC-DATA
RPTERR JMP SNDERR       **
GETHEX JMP GHEX
OUTADR JMP OTADR
INDEC JMP GDEC
DOCMND JMP DOCMD
STAT JMP TCHECK
 FCB $39,$39,$39
 FCB $39,$39,$39

* Flex Cold-start entry

INITF LDS #STACK
 JSR FMSINT
 CLR CMDFLG
 CLR LSTTRM
 JSR FLXINI

* Flex Warm-start entry

MAIN LDS #STACK
 LDX #WARMS
 STX ESCRET
 LDX IHNDLR
 STX [IRQVEC]
 CLR NAMFLG
 BSR RESIO
 LDA LSTTRM
 CMPA TTYEOL
 BNE LCD9E
 INC LINPTR+1
 BRA CMDLOP
LCD9E LDA CMDFLG
 LBNE DOCMDX
 JSR FMSCLS
 BNE INITF
 BSR CMDLIN
CMDLOP JSR SKIPSP
 CMPA #CR
 BEQ LCD9E
LCDB9 LDX #SYSFCB
 INC DEFSYS
 JSR GFILN
 BCS ILLCMD
 LDX #SYSCMD
 BSR SRCHTB
 BEQ EXETAB
*LDX UCMDTB             ** HD  FLEX [NDRET TIL DC4-EPROM
*BEQ SRCDIR             **
*BSR SRCHTB             **
*BNE SRCDIR             **
 JMP USCMEX             ** HD  NYT PROGRAM FOR EPROM

EXETAB JMP [,X]

SRCDIR JSR LDCMD
 BRA MAIN

ILLCMD JSR WHAT         ** HD  KUN TIL TERMINAL
ILLCMX LDA #21

LCDDF STA ERRTYP
LCDE2 JSR PSTR
LCDE5 CLR LSTTRM
 BRA MAIN

RESIO LDX OUTCH2+1
 STX OUTCH+1
 LDX INCH2+1
 STX INCH+1
 CLR INSW
 CLR OUTSW
 CLR SPECIO
 CLR INFIL
 CLR OUTFIL
LCE06 RTS

SRCHTB LDY #SYSFCB+4
SRCTL LDA ,Y+
 BEQ SRCSE
 CMPA #$5F
 BLS SRCUC
 SUBA #$20
SRCUC CMPA ,X+
 BEQ SRCTL
 LEAX -1,X
SRCSK LDA ,X+
 BNE SRCSK
 LEAX 2,X
 LDA ,X
 BNE SRCHTB
 COMA
 RTS
SRCSE LDA 0,X+
 BNE SRCSK
 RTS

CMDLIN LDX PROMT Point at promt text
PCMLIN JSR PSTR
GETLIN LDX #LINBUF
 STX LINPTR
CMDLNX CLR EscFlg
 JSR GETCH
 CMPA TTYDEL
 BEQ CMDLIN
 CMPA TTYBS
 BEQ CMDLBS
 CMPA #CR
 BEQ CMDLCH
 CMPA #SP-1
 BLS CMDLNX
 CMPX #LINEND
 BEQ CMDLER
CMDLCH STA ,X+
 CMPA #CR
 BNE CMDLNX
 RTS
CMDLER LDA TTYBS
 BSR LCE79
 BRA CMDLBE
CMDLBS CMPX #LINBUF
 BEQ CMDLIN
 LEAX -1,X
CMDLBE LDA TTYBE
 CMPA #BS
 BNE LCE74
 LDA #SP
 BSR LCE79
 LDA TTYBE
LCE74 BSR LCE79
 BRA CMDLNX
LCE79 JMP LCF65

PSTR BSR CRLF
 BRA PDATA

PDATAL BSR PUTCH
PDATA LDA ,X+
 CMPA #EOT
 BNE PDATAL
 RTS

ESCHEK JSR STAT
 BEQ LCEE0
 JSR [TINCH]
 CMPA TTYESC
 BNE LCEE0
PAUSE CLR CURLIN
PAUSEL JSR [TINCHE]
 CMPA TTYESC
 BEQ LCEE0
*
* INTERRUPT DRIVEN PAUSE
*
*ESCHEK LSR EscFlg Any Escape ?
* BCC LCEE0 No
*PAUSE CLR CURLIN Reset line count
*PAUSEL LSR EscFlg Pause exit ?
* BCS LCEE0 Yep
* JSR STAT Any key ?
* BEQ PAUSEL No
* JSR [TINCH] Get it

 CMPA #CR Display abort ?
 BNE PAUSEL No
 CLR LSTTRM
 JMP [ESCRET]

CRLF BSR NEWLIN
DOCRLF LDA #CR
 BSR PUTCH
LCEE4 LDA #LF
 BSR PUTCH
 PSHS B
 LDB TTYNL
 BEQ LCEF5
LCEEF CLRA
 BSR PUTCH
 DECB
 BNE LCEEF
LCEF5 PULS B,PC

NEWLIN LDA SPECIO
 BNE LCEE0
 BSR ESCHEK
 LDA TTYDP
 BEQ LCEE0
 CMPA CURLIN
 BHI LCEE0
 CLR CURLIN
 LDA TTYPS
 BEQ LCECF
 BSR PAUSE
LCECF PSHS B
 LDB TTYEJ
 BEQ LCEDB
LCED6 CLR CURLIN
 BSR LCEE4
 DECB
 BNE LCED6
LCEDB PULS B,PC
LCEE0 RTS

PUTCH TST SPECIO
 BNE LCF65
 CMPA #SP-1
 BHI LCF4E
 CLR CURCLM
 CMPA #LF
 BNE LCF65
 BSR NEWLIN
 INC CURLIN
 LDA #LF
 BRA LCF65
LCF4E INC CURCLM
 PSHS A
 LDA TTYWD
 BEQ LCF63
 CMPA CURCLM
 BHS LCF63
 BSR CRLF
 INC CURCLM
LCF63 PULS A
LCF65 PSHS A
 TST OUTSW
 BNE LCF7F
 TST OUTFIL
 BEQ LCF75
 BSR LCF28
 PULS A,PC
LCF75 TST INFIL
 BNE LCF82
 JSR OUTCH
 PULS A,PC
LCF7F JSR OUTCH2
LCF82 PULS A,PC

GETCH LDA INSW
 BNE LCF19
 LDA INFIL
 BEQ LCF14
 BSR LCF20
 TST FILEKO
 BEQ LCF1C
 TST OUTFIL
 BEQ LCF1C
 BSR LCF65
 BRA LCF1C
LCF14 JSR INCH
 BRA LCF1C
LCF19 JSR INCH2
LCF1C CLR CURLIN
 RTS

LCF20 PSHS X
 LDX INFIL
 BRA LCF2E

LCF28 PSHS X
 LDX OUTFIL
LCF2E JSR FMS
 BNE LCF37
 PULS X,PC
LCF37 CLR OUTFIL
 JSR RERR
 JMP WARMS

OTDEC LEAX 2,X
 PSHS B,X
 LDD #$400+'0-SP
 PSHS CC,A,B
 LDD -2,X
 LDX #DECTAB
OTDEK CLR ,S
OTDCNT INC ,S
 SUBD ,X
 BHS OTDCNT
 ADDD ,X++
 PSHS A
 LDA 1,S GET RESULTING DIGIT
 DECA ADJUST
 BNE OTNDIG NON ZERO
 SUBA 3,S CONVERT TO SPACE/ZERO DIGIT
 BEQ OTDIG NOT LEADING SPACE
 TST 4,S LEADING SPACE SUPRESS ?
 BNE OTDIG NO
 BRA OTDNXT
OTNDIG CLR 3,S FLAG NON ZERO DIGIT
OTDIG BSR OTDIGA
OTDNXT PULS A
 DEC 1,S
 BNE OTDEK
 LEAS 3,S
 TFR B,A
 PULS B,X
OTDIGA ADDA #'0 CONVERT TO ASCII
 JMP PUTCH

DECTAB FDB 10000,1000,100,10

* OUTADR - OUTPUT ADDR AT (X) IN HEX

OTADR BSR OTHEX HIGH BYTE
 LEAX 1,X POINT AT LOW PART

* OUTHEX - OUTPUT BYTE AT (X) IN HEX

OTHEX LDA ,X HIGH NIBBLE
 LSRA SHIFT DOWN
 LSRA
 LSRA
 LSRA
 BSR OTNIB
 LDA ,X LOW NIBBLE
 ANDA #$F
OTNIB ADDA #$90
 DAA
 ADCA #$40
 DAA
 JMP PUTCH

NXCH PSHS X
 LDX LINPTR
 LDA CURCHR
 STA PRVCHR
LD019 LDA ,X+
 CMPA #SP
 BNE LD023
 CMPA ,X
 BEQ LD019
LD023 STA CURCHR
 CMPA #CR
 BEQ LD032
 CMPA TTYEOL
 BEQ LD032
 STX LINPTR
LD032 PULS X

CLASCH CMPA #'^ Upper case ?
 BHI CLASUP
 CMPA #'@ @-^ ?
 BHS CLASSA
 CMPA #'9 Numerical range ?
 BHI CLASST
 CMPA #'0 0-9 ?
 BHS CLASSA
CLASST STA LSTTRM
 SEC
 RTS
CLASUP CMPA #'~ Alpha range ?
 BHI CLASST
 CMPA #'` `-~ ?
 BLO CLASST
CLASSA CLC
 RTS

GFILN JSR SKIPSP
 PSHS X
 LDD #$FF00
 STD 3,X
 STB 12,X
 LDD #21*256+8
 STA 1,X
 LEAX 4,X
 BSR NXCH
 BCS GFILNE
 CMPA #'9
 BHI GFILNA
 SUBA #'0
 STA -1,X
 BSR NXCH
 CMPA #'.
 BNE GFILNE
 BSR NXCH
 BCS GFILNE
GFILNA BSR GFILNT
 BCC GFILNE
 BNE GFILCD
 LDB #3
 BSR GFILNC
 BCC GFILNE
 BEQ GFILNE
GFILCD LDX ,S
 LDA 3,X
 BPL GFILNX
 LDA DEFSYS
 BEQ GFILWD
 LDA SYSDRV
 BRA GFILSD
GFILWD LDA WRKDRV
GFILSD STA 3,X
GFILNX CLR DEFSYS
 PULS X,PC
GFILNE COMA
 PULS X,PC

GFILNC JSR NXCH
 BCS GFILTE
GFILNT CMPA #'@
 BLO GFILTE
GFILTA CMPA MAPUP In range ?
 BLO GFILPC Yep
 SUBA MAPUP Map to uppercase
 ADDA #'A-1
GFILPC DECB
 BMI GFILTE
 STA ,X+
 JSR NXCH
 BCC GFILTA
 CMPA #'-
 BEQ GFILPC
 CMPA #'_
 BEQ GFILPC
GFILZL DECB
 BMI GFILZX
 CLR ,X+
 BRA GFILZL
GFILZX CMPA #'.
 SEC
 RTS
GFILTE CLRA
 RTS

SKIPSP PSHS X
 LDX LINPTR
LD0D9 LDA ,X+
 CMPA #SP
 BEQ LD0D9
 LEAX -1,X
 STX LINPTR
 PULS X,PC

SEXT PSHS U
 LDB 12,X
 BNE SEXTX
 CMPA #11
 BHI SEXTX
 LDU #EXTAB
 LDB #3
 MUL
 LEAU B,U
 LDA 2,U
 LDU ,U
 STU 12,X
 STA 14,X
SEXTX PULS U,PC

EXTAB FCC "BINTXTCMDBASSYSBAKSCRDAT"
 FCC "BACDIRPRTOUT"

GHEX PSHS U
 LDU #GHEXC
 LDA #16
 BRA GNUM

GHEXC CMPA #'9+1
 BLO GHEXCX
 ANDA #$5F
 SUBA #'A-'9-1
 CMPA #'0+16
GHEXCX RTS

GDECC CMPA #'9+1
 RTS

GDEC PSHS U
 LDU #GDECC
 LDA #10

GNUM LDX #0
 CLRB
 PSHS CC,A,B,X
GNUML JSR NXCH
 BCS GNUMT
 JSR ,U CHECK AND ADJUST
 BCC GNUMIL ILL DIGIT
 INC 2,S ONE DIGIT MORE
 SUBA #'0 CONVERT TO BINARY
 STA ,S
 LDB 1,S
 LDA 4,S
 MUL
 ADDB ,S ADD NEW DIGIT
 ADCA #0
 STB 4,S
 LDB 3,S
 STA 3,S
 LDA 1,S
 MUL
 ADDB 3,S
 STB 3,S
 ADCA #0
 BEQ GNUML NO OVERFLOW
GNUMIL JSR NXCH SKIP REST OF NUMBER
 BCC GNUMIL
 BRA GNUMEX
GNUMT CLRA TEMINATOR FOUND
GNUMEX LEAS 2,S SKIP WORKSPACE
 PULS B,X,U,PC

LDABIN LDX #0 NO LOAD OFFSET
 STX LDROFF

LDBIN PSHS Y,U
 CLR XFERFG
 LDX #SYSFCB
LDBINL JSR FMS
 BEQ LDBINT
 LDA 1,X ERROR CODE
 CMPA #8 EOF ?
 BNE LDBRER NO
 JSR SYSCLS
 PULS Y,U,PC
LDBINT CMPA #2
 BEQ LDBINR
 TSTA
 BEQ LDBINL
 CMPA #$16
 BNE BADBIN UNKNOWN RECORD ID
* READ TRANSFER RECORD
 JSR FMS HIGH ADDR
 BNE LDBRER
 STA XFERAD
 JSR FMS LOW
LDBRER BNE LD209
 STA XFERAD+1
 LDA #1 SET TRANSFER FLAG
 STA XFERFG
 BRA LDBINL
* READ BINARY DATA RECORD
LDBINR JSR FMS LOAD ADDR HIGH
 BNE LD209
 TFR A,B
 JSR FMS LOW
 BNE LD209
 EXG A,B
 ADDD LDROFF ADD LOAD OFFSET
 TFR D,Y
 JSR FMS GET DATA COUNT
 BNE LD209
 TSTA NULL RECORD ?
 BEQ LDBINL IGNORE
 TFR A,B
 LDA 34,X GET NEW REC ?
 BEQ RDNREC YEP
* SPECIAL FAST READ ROUTINE
* WILL ONLY USE FMS WHEN NESSARY
* SPEEDS LOAD UP, SPECIAL WHEN SPOOLER IS ACTIVE
RDFAST LEAU 64,X POINT AT DATA
 PSHS B
 LDB 34,X DATA INDEX
 CLRA
 LEAU D,U
 PULS B
RDFLOP LDA ,U+ TRANSFER DATA
 STA ,Y+
 INC 34,X MOVE INDEX
 DECB
 BEQ LDBINL DONE
 LDA 34,X NEW RECORD NEEDED ?
 BNE RDFLOP NO
RDNREC JSR FMS GET NEXT RECORD
 BNE LD209
 DEC 34,X ADJUST INDEX
 BRA RDFAST ALL OK

BADBIN LDX #BINBAD
 LDA #$FF
 BRA LD245

LD1E6 JSR FMS
 BEQ LD1FC
 LDA 1,X
 CMPA #8
 BNE LD1FF
 LEAS 2,S
SYSCLS LDA #4
 STA ,X
 JSR FMS
 BNE LD209
LD1FC CLRA
 RTS
LD1FF STA ERRTYP
 CMPA #4 FILE NOT FOUND ?
 BNE LD209
 SEC
 RTS
LD209 BSR RERR
 JMP LCDE5

GETCML INC NAMFLG
 JSR LDABIN
GETCMD CLRA DEFAULT BIN
 BSR LD247
 BCC GETCML NAME OK
 LDB NAMFLG
 LBEQ ILLCMD
 JMP WARMS

LDCMD LDA #2
 BSR LD253
 JSR LDABIN

EXECMD LDB XFERFG
 BEQ LD23F
 JMP [XFERAD]

LD23F LDX #NOTFR
 LDA #$81
LD245 JMP LCDDF

FNDNOT JSR NOTFUN       ** HD  KUN TIL TERMINAL
FNDNTX LDA #4
 BRA LD245

LD247 PSHS A
 LDX #SYSFCB
 JSR GFILN
 PULS A
 BCS LD26D
LD253 LDX #SYSFCB
 JSR SEXT
 LDA #1
 STA ,X
 BSR LD1E6
 BCS FNDNOT
 COM 59,X
 CLRA
 RTS
LD26D LDA LSTTRM
 CMPA #CR
 BEQ LD27B
 CMPA TTYEOL
 LBNE ILLCMD
LD27B SEC
LD27D RTS

RERR LDA 1,X
 STA ERRTYP
 BEQ LD27D
 PSHS X,Y
 JSR RESIO
 LDY ERRVEC
 BNE LD298
 CMPA #16 DRIVES NOT READY
 BEQ LD2EC
LDNOT LDY #ERRFIL
LD298 LDX #SYSFCB
 LDA 2,X
 BEQ LD2A8
 LDA #4
 STA ,X
 JSR FMS
 BNE LD2D6
LD2A8 LDX #SYSFCB+4
 LDB #11
 BSR COPYTX
 LEAX -4,X
 LDA SYSDRV
 STA 3,X
 LDA #1
 STA ,X
 JSR FMS
 BNE LD2D6
 LDB ERRTYP
 DECB
 LSRB
 LSRB
 INCB
 CLRA
 STD 32,X
 LDA #21
 STA ,X
 JSR FMS
 BEQ LD2F4
LD2D6 LDX #DSKERR
 JSR PSTR
 LDB ERRTYP
 CLRA
 PSHS D
 LEAX ,S
 CLRB
 JSR OTDEC
 PULS D,X,Y,PC
LD2EC LDX #SYSFCB      ** RET HD
 LDA 3,X
 CMPA #0
 LBNE LDNOT
 LDX #NOTRDY      **LD2EC
 JSR PSTR
 PULS X,Y,PC
LD2F4 JSR CRLF
 LDA ERRTYP
 DECA
 ANDA #3
 LDB #63
 MUL
 ADDB #4
 STB 34,X
LD308 JSR FMS
 BNE LD2D6
 JSR PUTCH
 CMPA #CR
 BNE LD308
 LDA #4
 STA ,X
 JSR FMS
 PULS X,Y,PC

COPYTX PSHS X,Y
COPYTL LDA ,Y+
 STA ,X+
 DECB
 BNE COPYTL
 PULS X,Y,PC

DOCMD PULS D SAVE USER PC
 STD CALLPC
 PSHS U FOR XBASIC INTERFACE (RENUMBER)
LD32D STS CALLSP
 CLR ERRTYP
 INC CMDFLG
 JMP LCDB9

DOCMDX CLR CMDFLG
 LDS CALLSP
 LDB ERRTYP
 PULS U GET USER U-REG BACK (XBASIC)
 JMP [CALLPC] RETURN

ADBX ABX
 RTS

MONCMD LDA TASK01 SPOOLER ACTIVE ?
 BNE LD353
 JMP [MONITR]

LD353 LDX #SYSFCB
 LDA #27
 STA 1,X
 JSR RERR
 JMP MAIN

ERRFIL FCC "ERRORS",0,0,"SYS"

BINBAD FCC "D}lig bin{rfil ",EOT

 ORG $D370

SNDERR TST BERRVC
 BEQ ORDERR
 LDX BERRVC
 CMPX #ERROR
 LBEQ ERROR
SNDER1 JSR SETL25
 LDX #$CC1F               ** HD  ???????????????????
 JSR RERR
 JSR INCHNO
 JSR CLRL25
 CLR ECHO
 LDX BERRVC
 STX INVEC
 RTS
ORDERR JMP RERR

 ORG $D39D

ERRRTN COM ECHO
 LDX #INHUP
 STX INVEC
 LDA #$0A
 PULS X,PC

TOUTPT JMP [MOUTCH]
TSTAT  JMP [MICHEC]
TINPUT JMP [MINCHE]
TCHNO  JMP [MINCH]
IRQRUT JMP $C700    IRQ ROUTINE FOR PRINTER SPOOL

DUMRTS RTS

** SET VIA TIMER ON
*
TM_ON LDA #%10100000
 STA VIA+$E
 LDD TIMCON
 STA VIA+$8
 STB VIA+$9
 NOP
 NOP
 NOP
 RTS

** SET VIA TIMER OFF
*
TM_OFF LDA #%00100000
 STA $E07E
 RTS

** INIT VIA TIMER FOR PRINTER SPOOL
*
TM_INT LDX #$0E0E
 STX TIMCON
 BRA TM_ON

 ORG $D3E0

BASRTS JMP BASRTN
IRQCLR FDB CLTIRQ
TINCH  FDB TCHNO

IHNDLR FDB IRQRUT
SWIVEC FDB SWI3V
IRQVEC FDB IRQV
TIMOFF FDB TM_OFF
TIMON  FDB TM_ON
TIMINT FDB TM_INT
MONITR FDB WARMS
TRMINT FDB DUMRTS
TCHECK FDB TSTAT
TOUTCH FDB TOUTPT
TINCHE FDB TINPUT

FLXINI JMP BOTBEG

*************  FMS  *************
*
 ORG $D400

FMSINT JMP FMSINI
FMSCLS JMP FMSCLO
FMS JMP FILMNG

FCBASE RMB 2
CURFCB RMB 2
LD40D  RMB 2
LD40F  RMB 2
LD411  RMB 1
LD412  RMB 1
LD413  RMB 2
LD415  RMB 2
LD417  RMB 1
LD418  RMB 2
LD41A  RMB 1
LD41B  RMB 2
LD41D  RMB 4*6

 ORG $D435

VRFYFG FCB $FF

SURTAB FDB $0000,$0000

FMSINI JSR DINIT
 LDX #FCBASE
 LDB #$0A
 BSR LD455
 LDX #$0005
 STX LD413
 STX LD415
 CLR LD41A

LD450 LDX #LD41B
 LDB #$1A

LD455 CLR 0,X+
 DECB
 BNE LD455
 JMP CLRTRP

FMSCLO JSR TRAPSW

LD460 LDX FCBASE
 BEQ LD450
 LEAX -28,X
 STX CURFCB
 PSHS Y
 JSR CLSFIL
 PULS Y
 BCC LD460
 LDX CURFCB
 CLR 2,X
 JSR CLRTRP
 LDB #$FF
 RTS

FILMNG TST TASK01
 BEQ LD487
 JSR TRAPSW

LD487 PSHS B,Y
 STX CURFCB
 CLR 1,X
 LDB 0,X
 BNE LD4B4

FMCOD0 LDB 2,X
 BEQ LD4B0
 CMPB #2
 BEQ LD4AB
 JSR RDBYTE

LD49D LDX CURFCB
 BCS LD4C8
 TST TASK01
 BNE LD4CA
 CLRB
 PULS B,Y
 RTS

LD4AB JSR WRBYTE
 BRA LD49D

LD4B0 LDB #$12
 BRA LD4C8

LD4B4 CMPB #$16
 BLS LD4BC
 LDB #1
 BRA LD4C8

LD4BC DECB
 ASLB
 LDX #FMCOD1
 JSR [B,X]
 LDX CURFCB
 BCC LD4CA

LD4C8 STB 1,X

LD4CA JSR CLRTRP
 TST 1,X
 PULS B,Y
 RTS

FMCOD1 FDB OPENRD
FMCOD2 FDB OPENWR
FMCOD3 FDB OPNUPD
FMCOD4 FDB CLSFIL
FMCOD5 FDB REWFIL
FMCOD6 FDB OPNDIR
FMCOD7 FDB GETINF
FMCOD8 FDB PTINFR
FMCOD9 FDB RDSECT
FMCD10 FDB WRSECT
FMCD11 FDB WRPROT
FMCD12 FDB DELETE
FMCD13 FDB RENAME
FMCD14 FDB VACANT
FMCD15 FDB NXTSEC
FMCD16 FDB OPNSIR
FMCD17 FDB GETRND
FMCD18 FDB PUTRND
FMCD19 FDB XINDEX
FMCD20 FDB FINDRV
FMCD21 FDB POSREC
FMCD22 FDB BACKUP

LD4FE BSR LD520
 BNE LD507
 LDB #2
 ORCC #1
 RTS

LD507 STD 0,X
 LDX 0,X
 CLR 0,X
 CLR 1,X
 RTS

LD510 BSR LD520
 BEQ LD519
 LDB #$0D
 ORCC #1
 RTS

LD519 LDD [0,X]
 STD 0,X
 ANDCC #$FE
 RTS

LD520 LDD CURFCB
 ADDD #$001C
 LDX #FCBASE

LD529 LDY 0,X
 BNE LD531
 ANDCC #$FB
 RTS

LD531 CMPD 0,X
 BNE LD537
 RTS

LD537 LDX 0,X
 BRA LD529

LD53B LDX CURFCB
 CLRA
 CLRB
 BSR LD544
 LDB #$2F

LD544 STA 17,X
 LEAX 1,X
 DECB
 BNE LD544
 RTS

LD54D LDX CURFCB
 LDB #$0B

LD552 LDA 4,X
 STA 36,X
 LEAX 1,X
 DECB
 BNE LD552
 RTS

LD55D LDX CURFCB
 LDB #$0B

LD562 LDA 4,X
 ORA #$20
 PSHS A
 LDA 36,X
 ORA #$20
 CMPA 0,S+
 BNE LD576
 LEAX 1,X
 DECB
 BNE LD562

LD576 RTS

GETRND LDX CURFCB
 LDB 2,X
 LSRB
 BCC LD5F8
 LDB 35,X
 JMP LD608

LD585 LDX CURFCB
 LDB 34,X
 INC 34,X
 ABX
 STA 64,X
 INCB
 BNE LD5B4
 ORCC #1
 RTS

PUTRND LDX CURFCB
 LDB 2,X
 ANDB #3
 CMPB #3
 BNE LD5F8
 ORB #$80
 STB 2,X
 LDB 15,X
 BITB #$80
 BNE LD5B7
 LDB 35,X
 ABX
 STA 64,X

LD5B4 ANDCC #$FE
 RTS

LD5B7 LDB #$0B
 ORCC #1
 RTS

RDBYTE LDA 59,X
 BMI LD5FD
 BEQ LD5CA
 DEC 59,X
 LDA #$20
 BRA LD5E7

LD5CA BSR LD5FD
 BCS LD5E9
 CMPA #$18
 BHI LD5E7
 BEQ LD5CA
 CMPA #9
 BNE LD5E4
 BSR LD5FD
 BCS LD5E9
 LDX CURFCB
 STA 59,X
 BRA RDBYTE

LD5E4 TSTA
 BEQ LD5CA

LD5E7 ANDCC #$FE

LD5E9 RTS

REWFIL JSR LDAC8
 BCS LD5F8
 BITA #1
 BEQ LD5F8
 STA 0,X
 JMP LD9A9

LD5F8 LDB #$12
 ORCC #1
 RTS

LD5FD LDX CURFCB
 LDB 34,X
 BEQ LD60F
 INC 34,X

LD608 ABX
 LDA 64,X
 ANDCC #$FE
 RTS

LD60F BSR LD614
 BCC LD5FD
 RTS

LD614 LDX CURFCB
 LDD 64,X
 INC 33,X
 BNE LD622
 INC 32,X

LD622 CMPD #$0000
 BEQ LD644

LD628 STD 30,X
 PSHS A
 LDA #4
 STA 34,X
 PULS A
 BSR RDSECT
 BCC LD648
 BITB #$80
 BEQ LD640
 LDB #$10
 BRA LD646

LD640 LDB #9
 BRA LD646

LD644 LDB #8

LD646 ORCC #1

LD648 RTS

RDSECT BSR LD670
 LDX CURFCB
 JSR DRIVE
 BCS LD665

LD653 BSR LD666
 JSR DREAD
 BNE LD65D
 ANDCC #$FE
 RTS

LD65D PSHS B
 BSR LD678
 PULS B
 BCC LD653

LD665 RTS

LD666 LDX CURFCB
 LDD 30,X
 LEAX 64,X
 RTS

LD670 CLRA
 STA LD411
 STA LD412
 RTS

LD678 BITB #$10
 BNE LD68D
 BITB #$80
 BNE LD6A4
 LDB LD411
 INCB
 CMPB #5
 BEQ LD68D
 STB LD411
 BRA VACANT

LD68D CLR LD411
 LDB LD412
 INCB
 CMPB #7
 BEQ LD6A4
 STB LD412
 LDX CURFCB
 JSR RESTOR

VACANT ANDCC #$FE
 RTS

LD6A4 ORCC #1
 RTS

WRSECT BSR LD670
 LDX CURFCB
 JSR DRIVE
 BCS LD6D1

LD6B1 LDX CURFCB
 BSR LD666
 JSR DWRITE
 BNE LD6C5
 LDA VRFYFG
 BEQ LD6F7
 JSR DVERFY
 BEQ LD6F7

LD6C5 BITB #$40
 BNE LD6D4
 PSHS B
 BSR LD678
 PULS B
 BCC LD6B1

LD6D1 RTS
 LDB #$20

LD6D4 ORCC #1
 RTS

WRBYTE LDX CURFCB
 LDB 59,X
 BMI LD71C
 CMPA #$20
 BNE LD6F2
 INCB
 STB 59,X
 CMPB #$7F
 BNE LD6F7
 BRA LD6FA

LD6ED BSR LD6FA
 BCC WRBYTE
 RTS

LD6F2 TSTB
 BEQ LD71C
 BRA LD6ED

LD6F7 ANDCC #$FE
 RTS

LD6FA PSHS A
 CMPB #1
 BNE LD704
 LDA #$20
 BRA LD714

LD704 LDA #9
 BSR LD71C
 PULS A
 BCS LD71B
 PSHS A
 LDX CURFCB
 LDA 59,X

LD714 CLR 59,X
 BSR LD71C
 PULS A

LD71B RTS

LD71C LDX CURFCB
 LDB 2,X
 CMPB #2
 LBNE LD5F8
 LDB 34,X
 CMPB #4
 BNE LD736
 PSHS A
 BSR LD753
 PULS A
 BCS LD745

LD736 JSR LD585
 BCC LD745
 LDB #4
 LDX CURFCB
 STB 34,X
 ANDCC #$FE

LD745 RTS

LD746 LDX CURFCB
 CLRA
 CLRB
 STD 32,X
 STD 66,X
 BRA LD77A

LD753 LDB 18,X
 BNE LD77A
 LDB 23,X
 BEQ LD7A1
 CLR 23,X
 BSR LD7A1
 BCS LD78E
 BSR LD746
 BCS LD78E
 BSR LD746
 BCS LD78E
 LDX CURFCB
 LDB #2
 STB 23,X
 LDD 17,X
 JMP LDC99

LD77A BSR LD78A
 LDX CURFCB
 STD 64,X
 JSR WRSECT
 BCC LD7A1
 JMP LDBD8

LD78A BSR LD78F
 LDD 0,X

LD78E RTS

LD78F LDX CURFCB
 LDB 3,X
 LDA #6
 MUL
 LDX #LD41D
 ABX
 STX LD41B
 TST 0,X
 RTS

LD7A1 BSR LD78A
 BNE LD7AA
 LDB #7

LD7A7 ORCC #1
 RTS

LD7AA LDX CURFCB
 STD 19,X
 TST 18,X
 BNE LD7B8
 STD 17,X

LD7B8 INC 22,X
 BNE LD7C0
 INC 21,X

LD7C0 TST 23,X
 BEQ LD7D0
 JSR LDC5A
 BCS LD7A7
 LDX CURFCB
 LDD 19,X

LD7D0 JSR LD628
 BCS LD7A7
 LDX CURFCB
 LDD 64,X
 PSHS A,B
 BSR LD78F
 PULS A,B
 STD 0,X
 BNE LD7EF
 CLR 2,X
 CLR 3,X
 CLR 4,X
 CLR 5,X
 BRA LD7F7

LD7EF LDY 4,X
 LEAY -1,Y
 STY 4,X

LD7F7 CLRA
 LDX CURFCB
 INC 33,X
 BNE LD803
 INC 32,X

LD803 CLRB

LD804 STA 64,X
 LEAX 1,X
 DECB
 BNE LD804
 LDX CURFCB
 LDD 32,X
 STD 66,X
 ANDCC #$FE
 RTS

OPNSIR CLRB
 PSHS B
 LDB #3
 BRA LD82D
 LDX LD415
 STX LD413

OPNDIR LDB LD413
 PSHS B
 LDB LD413+1

LD82D LDX CURFCB
 STB 65,X
 PULS B
 STB 64,X
 CLR LD418
 CLRB
 STB 34,X
 RTS

GETINF LDX CURFCB
 LDB 34,X
 BNE LD866
 JSR LD614
 BCS LD87E
 LDX CURFCB
 TST LD418
 BNE LD85B
 LDD #$0005
 STD LD418

LD85B LDA #$10
 STA 34,X
 LDD 30,X
 STD 47,X

LD866 LDA 34,X
 STA 49,X
 LDB #$18

LD86E PSHS B,X
 JSR LD5FD
 PULS B,X
 STA 4,X
 LEAX 1,X
 DECB
 BNE LD86E
 ANDCC #$FE

LD87E RTS

PTINFR LDX CURFCB
 LDA 49,X
 STA 34,X
 LDB #$18

LD88A PSHS B,X
 LDA 4,X
 JSR LD71C
 PULS B,X
 LEAX 1,X
 DECB
 BNE LD88A
 JMP WRSECT

LD89B LDX CURFCB
 LDA 3,X
 STA 35,X
 LDA LD417
 TST LD41A
 BNE LD8DC
 STA 3,X
 LDX LD415
 STX LD413

LD8B3 CMPX #$0005
 BEQ LD8C4
 BSR LD8DC
 BLS LD8F3
 LDX LD418
 STX LD413
 BRA LD8B3

LD8C4 LDX CURFCB
 LDA 35,X
 STA 3,X
 BPL LD8DC

LD8CE JSR FINDRV
 BCS LD909
 BSR LD8DC
 BLS LD8F3
 JSR LDDC5
 BRA LD8CE

LD8DC LDX CURFCB
 CLR LD41A
 JSR LD54D
 JSR OPNDIR

LD8E8 JSR GETINF
 BCC LD8F4
 CMPB #8
 BEQ LD909
 ORCC #1

LD8F3 RTS

LD8F4 LDX CURFCB
 LDA 4,X
 BEQ LD907
 BPL LD8FF
 BSR LD90E

LD8FF JSR LD55D
 BNE LD8E8
 ANDCC #$FE
 RTS

LD907 BSR LD90E

LD909 ANDCC #$FB
 ANDCC #$FE
 RTS

LD90E LDA 51,X
 BNE LD91F
 LDD 47,X
 STD 50,X
 LDA 49,X
 STA 52,X

LD91F RTS

LD920 JSR LD78F
 BNE LD93C
 BSR LD93F
 BCS LD93E
 LDB #6
 LDY CURFCB
 LDX LD41B

LD932 LDA 93,Y
 LEAY 1,Y
 STA 0,X+
 DECB
 BNE LD932

LD93C ANDCC #$FE

LD93E RTS

LD93F JSR OPNSIR
 JSR LD614
 BCS LD94F
 LDX CURFCB
 LDB #$10
 STB 34,X

LD94F RTS

LD950 JSR LD78F
 BSR LD93F
 BCS LD94F
 LDB #6
 LDY CURFCB
 LDX LD41B

LD960 LDA 0,X+
 STA 93,Y
 LEAY 1,Y
 DECB
 BNE LD960
 JSR WRSECT
 BCC LD94F
 JMP LDBD8

WRPROT LDX CURFCB
 LDA #2
 STA 2,X
 LDD 47,X
 STD 30,X
 JSR RDSECT
 BCS LD98C
 JSR PTINFR
 BCC LD98E
 JMP LDBD8

LD98C LDB #$0A

LD98E RTS

OPENRD JSR LD4FE
 BCS LD9D1
 JSR LD89B
 BCS LD9D1
 BNE LD9D6
 LDX CURFCB
 TST LD41A
 BEQ LD9A9
 LDA 15,X
 BITA #$20
 BNE LD9D2

LD9A9 JSR LDCD8
 BCS LD9D8
 LDD 17,X
 STD 64,X
 JSR LDA8D
 LDB 23,X
 BEQ LD9CF

LD9BC PSHS B
 JSR LD614
 PULS B
 BCS LD9D1
 DECB
 BNE LD9BC
 LDX CURFCB
 CLRB
 STB 34,X

LD9CF ANDCC #$FE

LD9D1 RTS

LD9D2 LDB #$11
 BRA LD9D8

LD9D6 LDB #4

LD9D8 PSHS B
 JSR LD510
 PULS B
 ORCC #1
 RTS

OPENWR LDX CURFCB
 TST 3,X
 BPL LD9F1
 JSR FINDRV
 BCC LD9F1
 LDB #$10
 RTS

LD9F1 JSR LD4FE
 BCS LD9D8
 JSR LD53B
 JSR LD920
 BCS LD9D8
 JSR LD89B
 BCS LD9D8
 BNE LDA09
 LDB #3
 BRA LD9D8

LDA09 JSR LDCD8
 BCS LD9D8
 LDX CURFCB
 LDB #$0A

LDA13 CLR 15,X
 LEAX 1,X
 DECB
 BNE LDA13
 LDX CURFCB
 LDD 50,X
 BEQ LDA56
 STD 47,X
 LDA 52,X
 STA 49,X
 LDD SYSMTH
 STD 25,X
 LDA SYSYR
 STA 27,X
 LDA 3,X
 LDX #SURTAB
 LDA A,X
 LDX CURFCB
 STA 24,X
 JSR TIDRUT   Hopp f|r tid till diskfil
 JSR WRPROT
 BCS LD9D8
 BSR LDA8D
 LDA #4
 STA 34,X
 ANDCC #$FE
 RTS

LDA56 LDX CURFCB
 CLR 23,X
 INC 18,X
 LDD 47,X
 JSR LD628
 BCS LDA74
 JSR LD77A
 BCS LDA74
 JSR WRSECT
 BCC LDA77
 JSR LDBD8

LDA74 JMP LD9D8

LDA77 LDX CURFCB
 LDD 30,X
 STD 50,X
 LDA #$10
 STA 52,X
 JSR LD950
 BCS LDA74
 JMP LDA09

LDA8D LDX CURFCB
 LDA 0,X
 STA 2,X
 CLR 0,X
 CLR 59,X
 CLRA
 STA 34,X
 RTS

NXTSEC BSR LDAC8
 BCS LDAB0
 CLR 0,X
 LSRA
 LBCS LD614
 LDB #4
 STB 34,X
 ANDCC #$FE

LDAB0 RTS

LDAB1 LDX CURFCB
 LDA 2,X
 CMPA #$83
 BNE LDAC5
 LDA #3
 STA 2,X

LDABE JSR WRSECT
 LBCS LDBD8

LDAC5 ANDCC #$FE
 RTS

LDAC8 BSR LDAB1
 BCS LDAD9
 LDX CURFCB
 LDA 2,X
 CMPA #3
 BLS LDAC5
 LDB #$12
 ORCC #1

LDAD9 RTS

CLSFIL BSR LDAC8
 BCS LDB0F
 CMPA #2
 BEQ LDAEA

LDAE2 LDX CURFCB
 CLR 2,X
 JMP LD510

LDAEA LDA 18,X
 BNE LDAF4
 JSR LDBBE
 BRA LDB0D

LDAF4 BSR LDABE
 BCS LDB0F
 LDX CURFCB
 TST 23,X
 BEQ LDB05
 JSR LDCAF
 BCS LDB0F

LDB05 JSR WRPROT
 BCS LDB0F
 JSR LD950

LDB0D BCC LDAE2

LDB0F RTS

OPNUPD JSR OPENRD
 BCS LDB3D
 JSR LD614
 BCS LDB3D
 LDA #3
 BRA LDB36

XINDEX JSR OPENRD
 BCS LDB3D
 LDX CURFCB
 LDA 15,X
 BITA #$80
 BNE LDB3E
 LDD 19,X
 JSR LD628
 BCS LDB3D
 LDA #2

LDB36 LDX CURFCB
 STA 2,X
 ANDCC #$FE

LDB3D RTS

LDB3E LDB #$0B
 ORCC #1
 RTS

RENAME BSR LDB7A
 JSR LD89B
 BCS LDB74
 BEQ LDB70
 LDX CURFCB
 LDB #$0B

LDB51 LDA 36,X
 STA 4,X
 LEAX 1,X
 DECB
 BNE LDB51
 BSR LDBAA
 BCS LDB74
 LDX CURFCB
 LDA 15,X
 BITA #$80
 BNE LDB3E
 BITA #$60
 BNE LDB75
 BSR LDB7A
 BRA LDBC5

LDB70 LDB #3
 ORCC #1

LDB74 RTS

LDB75 LDB #$0C
 ORCC #1
 RTS

LDB7A LDX CURFCB
 LDA #$0B
 STA LD411

LDB82 LDA 4,X
 LDB 53,X
 STA 53,X
 STB 4,X
 LEAX 1,X
 DEC LD411
 BNE LDB82
 LDX CURFCB
 LDA 12,X
 BNE LDBA6
 LDB #3

LDB9C LDA 61,X
 STA 12,X
 LEAX 1,X
 DECB
 BNE LDB9C

LDBA6 LDX CURFCB
 RTS

LDBAA BSR LDB7A

LDBAC JSR LD89B
 BCS LDBB8
 BNE LDBB9
 LDX CURFCB
 ANDCC #$FE

LDBB8 RTS

LDBB9 LDB #4
 ORCC #1
 RTS

LDBBE LDX CURFCB
 LDA #$FF
 STA 4,X

LDBC5 JSR WRPROT
 LDX CURFCB
 LDA #0
 STA 2,X
 RTS

LDBD0 STD 64,X
 JSR WRSECT
 BCC LDBEC

LDBD8 BITB #$40
 BNE LDBE4
 BITB #$80
 BEQ LDBEA
 LDB #$10
 BRA LDBEA

LDBE4 LDB #$0B
 BRA LDBEA
 LDB #$0A

LDBEA ORCC #1

LDBEC RTS

DELETE JSR LD920
 BCS LDC50
 BSR LDBAC
 BCS LDC50
 LDX CURFCB
 LDA 15,X
 BITA #$80
 BNE LDC51
 BITA #$60
 BNE LDC55
 JSR LD78F
 LDX LD41B
 LDD 2,X
 BNE LDC1C
 LDX CURFCB
 LDD 17,X
 BEQ LDC48
 LDX LD41B
 STD 0,X
 BRA LDC30

LDC1C LDX CURFCB
 JSR LD628
 BCS LDC50
 LDX CURFCB
 LDD 17,X
 BEQ LDC48
 BSR LDBD0
 BCS LDC50

LDC30 LDX CURFCB
 LDD 19,X
 LDX LD41B
 STD 2,X
 LDX CURFCB
 LDD 21,X
 LDX LD41B
 ADDD 4,X
 STD 4,X

LDC48 JSR LDBBE
 BCS LDC50
 JSR LD950

LDC50 RTS

LDC51 LDB #$0B
 BRA LDC57

LDC55 LDB #$0C

LDC57 ORCC #1
 RTS

LDC5A LDD 30,X
 INCB
 CMPB 60,X
 BLS LDC66
 LDB #1
 INCA

LDC66 CMPD 19,X
 BNE LDC7A
 LDA 55,X
 CMPA #$FF
 BEQ LDC7A
 INCA
 STA 55,X
 ANDCC #$FE
 RTS

LDC7A BSR LDCAF
 BCS LDCAE
 LDX CURFCB
 LDA 58,X
 ADDA #3
 BNE LDC9E
 LDD 30,X
 CMPD 17,X
 BEQ LDC96
 LDB #$17
 ORCC #1
 RTS

LDC96 LDD 64,X

LDC99 STD 56,X
 LDA #4

LDC9E STA 58,X
 LDD 19,X
 STD 53,X
 LDA #1
 STA 55,X
 ANDCC #$FE

LDCAE RTS

LDCAF LDD 56,X
 JSR LD628
 BCS LDCAE
 LDX CURFCB
 TFR X,Y
 LDB 58,X
 NOP
 ABX
 LDB #3

LDCC3 LDA 53,Y
 LEAY 1,Y
 STA 64,X
 LEAX 1,X
 DECB
 BNE LDCC3
 JSR WRSECT
 BCC LDCAE
 JMP LDBD8

LDCD8 JSR OPNSIR
 JSR LD614
 BCS LDD26
 LDX CURFCB
 CLRA
 CLRB
 STD 32,X
 LDA 103,X
 STA 60,X
 CLRB

LDCEF CLR 64,X
 LEAX 1,X
 DECB
 BNE LDCEF
 LDX CURFCB
 ANDCC #$FE
 RTS

BACKUP LDX CURFCB
 LDA 23,X
 BEQ LDD22
 LDD 32,X
 SUBD #$0001
 BPL LDD10
 JMP LDDB2

LDD10 STD 32,X

POSREC JSR LDAC8
 BCS LDD26
 RORA
 BCC LDD22
 CLR 0,X
 LDA 23,X
 BNE LDD27

LDD22 LDB #$12
 ORCC #1

LDD26 RTS

LDD27 CLR LD411
 LDD 17,X
 LDY 32,X
 BEQ LDD9D
 JSR LDDB7
 BCS LDD26
 CLRA
 CLRB

LDD3A TST 2,X
 BEQ LDDB2
 ADDB 2,X
 ADCA #0
 STX LD40F
 LDX CURFCB
 CMPD 32,X
 BCC LDD7A
 LDX LD40F
 LEAX 3,X
 PSHS A
 LDA LD411
 INCA
 STA LD411
 CMPA #$54
 BEQ LDD68
 CMPA #$A8
 PULS A
 BEQ LDDB2
 BRA LDD3A

LDD68 PSHS B
 LDX CURFCB
 LDD 64,X
 BSR LDDB7
 BCS LDDB2
 PULS B
 PULS A
 BRA LDD3A

LDD7A SUBD 32,X
 LDX LD40F
 LDA 2,X
 PSHS B
 SUBA 0,S+
 DECA
 TFR A,B
 LDA 0,X
 ADDB 1,X
 LDX CURFCB
 BCS LDD97

LDD92 CMPB 60,X
 BLS LDD9D

LDD97 SUBB 60,X
 INCA
 BRA LDD92

LDD9D JSR LD628
 BCS LDDB6
 LDX CURFCB
 LDD 66,X
 CMPD 32,X
 BEQ LDDC2
 LDB #$19
 BRA LDDB4

LDDB2 LDB #$18

LDDB4 ORCC #1

LDDB6 RTS

LDDB7 JSR LD628
 BCS LDDC4
 LDX CURFCB
 LDB #$44
 ABX

LDDC2 ANDCC #$FE

LDDC4 RTS

LDDC5 LDX CURFCB
 LDB #$0B

LDDCA LDA 36,X
 STA 4,X
 LEAX 1,X
 DECB
 BNE LDDCA
 RTS

FINDRV LDX CURFCB
 LDA 3,X
 INCA
 CMPA #4
 BCC LDDEE
 STA 3,X
 BNE LDDE8
 JSR DCHECK
 BRA LDDEB

LDDE8 JSR DQUICK

LDDEB BCS FINDRV
 RTS

LDDEE LDB #$10
 ORCC #1
 RTS

 ORG $DE00

DREAD JSR DISKIO

DWRITE JSR DISKIO

DVERFY JSR DISKIO

RESTOR JSR VDRIVE

DRIVE JSR VDRIVE

DCHECK JSR VDRIVE

DQUICK JSR VDRIVE

DINIT JMP VCOLD

DDENS FCB $00

DSTEP FCB $00

LOADED FCB $00

CURDRV FCB $00

DRVTBL FCB $00,$00,$00,$00

TRKTBL FCB $00,$00,$00,$00

READ LBSR DRSEEK
 LDA #$88
 ORA DDENS
 ORA LOADED
 CLR LOADED

LDE3F NOP
 ORCC #$50
 STA COMREG
 LBSR DELAY1
 CLRB
 TST DDENS
 BEQ LDE6F
 PSHS Y,U
 LDU #DATREG
 LDY #DRVREG

LDE57 LDA 0,Y
 BMI LDE5F
 BEQ LDE57
 BRA LDE66

LDE5F LDA 0,U
 STA 0,X+
 DECB
 BNE LDE57

LDE66 LBSR TSTDEN
 ANDCC #$AF
 BITB #$9C
 PULS Y,U,PC

LDE6F LDA COMREG
 BMI RDEXIT
 BITA #2
 BNE LDE7E
 BITA #1
 BNE LDE6F
 BRA RDEXIT

LDE7E LDA DATREG
 STA 0,X+
 DECB
 BNE LDE6F

RDEXIT LBSR TSTDEN
 ANDCC #$AF
 BITB #$9C
 RTS

WRITE LBSR DRSEEK
 LDA #$A8
 ORA DDENS
 ORA LOADED
 CLR LOADED

WRITE1 NOP
 ORCC #$50
 STA COMREG
 LBSR DELAY1
 CLRB
 TST DDENS
 BEQ LDED1
 PSHS Y,U
 LDU #DATREG
 LDY #DRVREG

WLOOP1 LDA 0,Y
 BMI LDEC3
 BEQ WLOOP1
 BRA LDECA

LDEC3 LDA 0,X+
 STA 0,U
 DECB
 BNE WLOOP1

LDECA LBSR TSTDEN
 BSR LDEEB
 PULS Y,U,PC

LDED1 LDA COMREG
 BMI LDEE8
 BITA #2
 BNE LDEE0
 BITA #1
 BNE LDED1
 BRA LDEE8

LDEE0 LDA 0,X+
 STA DATREG
 DECB
 BNE LDED1

LDEE8 LBSR TSTDEN

LDEEB CLRA

WREXIT DECA
 LBNE WREXIT
 ANDCC #$AF
 BITB #$DC
 RTS

DVRIFY LDA #$88
 ORA DDENS
 ORCC #$50
 STA COMREG
 LBSR DELAY1
 LBSR TSTDEN
 ANDCC #$AF
 BITB #$98
 RTS

DRSEEK PSHS A,X
 STB SECREG
 BSR LDF64
 ORB CURDRV
 STB DRVREG
 CMPA TRKREG
 BEQ LDF60
 LDB CURDRV
 LDX #DRVTBL
 LDB B,X
 ANDB #2
 STB DSTEP
 BEQ LDF45
 ASL TRKREG
 ASLA

LDF45 STA DATREG
 LDA #4
 STA LOADED
 LDA #$18    18 = 6 mS, 19 = 12 mS, 1A = 20 mS, 1B = 30 mS
 STA COMREG
 BSR DELAY1
 JSR TBUSY
 BSR DELAY1
 TST DSTEP
 BEQ LDF62
 LSR TRKREG

LDF60 BSR DELAY1

LDF62 PULS A,X,PC

LDF64 BSR SDORDD
 BEQ LDF6F
 CMPB #$12
 BLS LDF73

LDF6C LDB #$40
 RTS

LDF6F CMPB #$0A
 BHI LDF6C

LDF73 LDB #0
 RTS

SDORDD BRN LDF8C
 STA 0,-S
 BEQ LDF84
 LDA CURDRV
 LDX #DRVTBL
 LDA A,X

LDF84 ANDA #1
 ASLA
 STA DDENS
 PULS A,PC

LDF8C CLR DDENS
 RTS

TSTDEN LBSR TBUSY
 PSHS B,X
 BITB #$10
 BEQ LDFA0
 LDB CURDRV
 LDX #DRVTBL
 INC B,X
LDFA0 PULS B,X,PC

DRQUIC PSHS X
 LDA 3,X
 STA DRVREG
 LDX #$161C

LCBC4 TST COMREG
 LBSR DELAY1
 LEAX -1,X
 BNE LCBC4
 LDB COMREG
 LBMI LCB98
 CLRA
 PULS X,PC
LCB98 COMB
 PULS X,PC

TBUSY LDB COMREG
 BITB #1
 BNE TBUSY
 RTS

DELAY1 BSR DELAY2

DELAY2 BSR DELAY3

DELAY3 BSR DLYEND

DLYEND RTS

 ORG $E100

POVER JSR PCRLF
 LDA #$E1
 JSR PGSTR
 LDX #GSTR2
PO1 LDA 0,X+
 BEQ PROEND
 JSR GOUTCH
 BRA PO1
PROEND JSR PCRLF
 LDA #$E1           STREG
 JSR PGSTR
 JSR PCRLF
 JSR PCRLF
 LDX #ENTMSG
 JSR PSTRNG
 LDB #$0A
 LDA #$04
CHBAK JSR PUTCHR
 DECB
 BNE CHBAK
 LDX #RAMSIZ
 JSR OUTDEC
 JSR PCRLF
 TST RTCBYT
 LBNE SSVDSK
M3000 JSR PCRLF
 JSR RTCSET
 LDY #DAYTAB-2
 LDB WEEKD+1        DAY
 ANDB #$0F
 ASLB
 LDX B,Y
 JSR PDATA0
 JSR PCRLF
 LDX #TIME
 LDA 0,X+           DATO
 JSR OUTCHR
 LDA 0,X++          DATO+-
 JSR OUTCHR
 LDA #$20
 JSR OUTCHR
 LDA 0,X+           MONTH HIGH
 RPT 4
 ASLA
 STA WRKSPC
 LDB 0,X++          MONTH LOW + -
 ANDB #$0F
 ORB WRKSPC
 CMPB #$10
 BLO LAB1
 SUBB #$06
LAB1 ASLB
 LDY #MONTAB-2
 PSHS X
 LDX B,Y
 JSR PDATA0         PRINT MONTH
 PULS X
 LDA #$20
 JSR OUTCHR
 LDA #$19
 JSR OUT2H
 LDA 0,X+
 RPT 4
 ASLA
 STA WRKSPC
 LDA 0,X++
 ANDA #$0F
 ORA WRKSPC
 JSR OUT2H
 JSR PCRLF
 LDX #STRNG
 JSR PDATA0
 LDX #RTCTIM
TIMOUT LDA 0,X+
 CMPX #WEEKD+1
 BEQ WEEKNR
 JSR OUTCHR
 BRA TIMOUT
WEEKNR JSR PCRLF
 LDX #STRNG1
 JSR PDATA0
 LDX #WEEKN
 LDA 0,X+
 JSR OUTCHR
 LDA 0,X
 JSR OUTCHR
 JSR PCRLF
FLEXDAT LDA TIME+1
 ANDA #$0F
 STA WRKSPC
 LDA TIME
 ANDA #$0F
 LDB #10
 MUL
 ADDB WRKSPC
 STB SYSMTH+1
 LDA TIME+4
 ANDA #$0F
 STA WRKSPC
 LDA TIME+3
 ANDA #$0F
 LDB #10
 MUL
 ADDB WRKSPC
 STB SYSMTH
 LDA TIME+7
 ANDA #$0F
 STA WRKSPC
 LDA TIME+6
 ANDA #$0F
 LDB #10
 MUL
 ADDB WRKSPC
 STB SYSMTH+2
SSVDSK LDA SYSBYT
 CMPA #$5A
 BEQ MRTS
 LDA #$0F
 CMPA MAXRAM
 BEQ LWSYS
 LDB #$10
 LDA #$0
 STB $FFF0
 BRA CLRP0
LWSYS TFR A,B
 STB $FFF0
CLRPX LDA #$04           END OF FILE
CLRP0 LDX #0
CLRP1 STA 0,X+
 CMPX #$1000
 BNE CLRP1
 INCB
 CMPB MAXRAM
 BHI CLLRTS
 STB $FFF0
 BRA CLRPX
CLLRTS LDA #$00
 STA $FFF0
MRTS RTS

ENTMSG FCC "Flex  - PO  -       K ram",4
STRNG FCC "Kl  ",0

DAYTAB FDB DAY1,DAY2,DAY3,DAY4
 FDB DAY5,DAY6,DAY7

MONTAB FDB MON1,MON2,MON3,MON4,MON5,MON6
 FDB MON7,MON8,MON9,MON10,MON11,MON12

STRNG1 FCC "Vecka ",0
DAY1 FCC "M}ndag",0
DAY2 FCC "Tisdag",0
DAY3 FCC "Onsdag",0
DAY4 FCC "Torsdag",0
DAY5 FCC "Fredag",0
DAY6 FCC "L|rdag",0
DAY7 FCC "S|ndag",0
MON1 FCC "januari",0
MON2 FCC "februari",0
MON3 FCC "mars",0
MON4 FCC "april",0
MON5 FCC "maj",0
MON6 FCC "juni",0
MON7 FCC "juli",0
MON8 FCC "augusti",0
MON9 FCC "september",0
MON10 FCC "oktober",0
MON11 FCC "november",0
MON12 FCC "december",0

 END XFER
